# BUGS script for analyzing the theta logistic population model
# Authors: C.W. Berg and M.W. Pedersen, 13/7-2010

model{

# Define prior densities for parameters
K     ~ dunif(1.0, 22000.0)
logr0    ~ dunif(-4.0, 2.0)
logtheta ~ dunif(-4.0, 2.0)
iQ ~ dgamma(0.0001,0.0001)
iR ~ dgamma(0.0001,0.0001)

# Transform parameters to fit in the model
r0 <- exp(logr0)
theta <- exp(logtheta)

# Initial state
x[1] ~ dunif(0,10)

# Loop over all states, Eq. 3 in paper
for(t in 1:(N-1)){
  mu[t] <- x[t] + exp(r0 * (1 - x[t]/K)* (x[t] - theta) )
  x[t+1] ~ dnorm(mu[t],iQ)
}

# Loop over all observations, Eq. 4 in paper
for(t in 1:(N)){
  y[t] ~ dnorm(x[t],iR)
}

}
